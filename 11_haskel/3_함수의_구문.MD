## 패턴 매칭(Pattern matching)
- 어떤 데이터가 따라야 할 패턴을 지정하거나, 그 패턴에 따라 데이터를 분해하기 위하여 사용
- 함수를 정의할 경우 다른 패턴에 대해 별도의 함수 내용(Body)을 만들 수 있다.

```haskell
lucky :: Int -> String
lucky 7 = "LUCKY NUMBER SEVEN!!"
lucky x = "Sorry, you're out of luck, pal!"
```

```haskell
sayMe :: Int -> String
sayMe 1 = "One!"
sayMe 2 = "Two!"
sayMe 3 = "Three!"
sayMe 4 = "Four!"
sayMe 5 = "Five!"
sayMe x = "Not between 1 and 5"
```

- 재귀적으로 팩토리얼 정의
```haskell
factorial :: Int -> Int
factorial 0 = 1
factorial n = n * factorial (n-1)
```

- 패턴 매칭 실패
```haskell
charName :: Char -> String
charName 'a' = "Albert"
charName 'b' = "Broseph"
charName 'c' = "Cecil"
ghci> charName 'a'
"Albert"
ghci> charName 'b'
"Broseph"
ghci> charName 'h'
"*** Exception: const.hs:(18,1)-(20,22): Non-exhaustive patterns in function charName
```

### 튜플의 패턴 매칭
```haskell
addVectors :: (Double, Double) -> (Double, Double) -> (Double, Double)
addVectors a b = (fst a + fst b, snd a + snd b)
```
```haskell
addVectors :: (Double, Double) -> (Double, Double) -> (Double, Double)
addVectors (x1, y1) (x2, y2) = (x1 + x2, y1 + y2)
ghci> :t addVectors
addVectors
  :: (Double, Double) -> (Double, Double) -> (Double, Double)
```

- 트리플에서의 함수 만들기
```haskell
first :: (a, b, c) -> a
first (x, _, _) = x

second :: (a, b, c) -> b
second (_, y, _) = y

third :: (a, b, c) -> c
third (_, _, z) = z
```

### 리스트와 리스트 통합의 패턴 매칭
```haskell
ghci> let xs = [(1,3),(4,3),(2,4),(5,3),(5,6),(3,1)]
ghci> [a+b | (a, b) <- xs]
[4,7,6,8,11,4]
```

- head 함수에 대한 구현체
```haskell
head' :: [a] -> a
head' [] = error "Can't call head on an empty list, dummy!"
head' (x:_) = x
ghci> head' [4,5,6]
4
ghci> head' "Hello"
'H'
```
> error 함수: 인자로 받은 문자열을 이용하여 런타임 에러를 생성

- 리스트 장황하게 출력하는 함수
  - (x:[])와 (x:y:[])는 [x], [x,y]랑 동일
  - (x:y:_)은 2 개 이상의 모든 리스트와 매칭 (_ 갯수 관계 없음)
```haskell
tell :: (Show a) => [a] -> String
tell [] = "The list is empty"
tell (x:[]) = "The list has one element: " ++ show x
tell (x:y:[]) = "The list has two elements: " ++ show x ++ " and " ++ show y
tell (x:y:_) = "This list is long. The first two elements are: " ++ show x ++ " and " ++ show y
```
```haskell
ghci> tell [1]
"The list has one element: 1"
ghci> tell [True, False]
"The list has two elements: True and False"
ghci> tell [1,2,3,4]
"This list is long. The first two elements are: 1 and 2"
ghci> tell []
"The list is empty"
```

### as-패턴
- 패턴에 따라 항목을 분해할 수 있게 해준다

```haskell
firstLetter :: String -> String
firstLetter "" = "Empty string, whoops!"
firstLetter all@(x:xs) = "The first letter of " ++ all ++ " is " ++ [x]
ghci> firstLetter "Dracula"
"The first letter of Dracula is D"
```

## 가드
- 파이프 라인 문자(|) 다음에 불리언 표현식이 나오고, 그 다음에 함수의 내용이 나타난다.
- Switch case문과 같은 역할
- otherwise - switch case문의 default

```haskell
max' :: (Ord a) => a -> a -> a
max' a b
  | a <= b = b
  | otherwise = a
```

```haskell
myCompare :: (Ord a) => a -> a -> Ordering
a `myCompare` b
  | a == b  = EQ
  | a <= b  = LT
  | otherwise = GT

ghci> 3 `myCompare` 2
GT
```

## WHERE
- 중간 계산 결과를 어떻게 저장하는지
```haskell
bmiTell' :: Double -> Double -> String
bmiTell' weight height
  | weight / height ^ 2 <= 18.5 = "You're underweight, you emo, you!"
  | weight / height ^ 2 <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"
  | weight / height ^ 2 <= 30.0 = "You're fat! Lose some weight, fatty!"
  | otherwise = "You're a whale, congratulations!"
```
```haskell
bmiTell' :: Double -> Double -> String
bmiTell' weight height
  | bmi <= 18.5 = "You're underweight, you emo, you!"
  | bmi <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"
  | bmi <= 30.0 = "You're fat! Lose some weight, fatty!"
  | otherwise = "You're a whale, congratulations!"
  where bmi = weight / height ^ 2
```

- 변수 정의
```haskell
bmiTell' :: Double -> Double -> String
bmiTell' weight height
  | bmi <= skinny = "You're underweight, you emo, you!"
  | bmi <= normal = "You're supposedly normal. Pffft, I bet you're ugly!"
  | bmi <= fat = "You're fat! Lose some weight, fatty!"
  | otherwise = "You're a whale, congratulations!"
  where bmi = weight / height ^ 2
        skinny = 18.5
        normal = 25.0
        fat = 30.0
```

### 범위
- 함수의 where 절에 정의한 변수들은 그 함수에서만 보이기 때문에, 다른 함수의 네임스페이스에 영향을 미치지 않는다.
- where 바인딩은 서로 다른 패턴의 함수 내용에 걸쳐 공유되지 않고, 마지막 함수 내용만이 where 바인딩에 의해 정의된 환영글을 볼 수 있다.
- X
```haskell
greet :: String -> String
greet "Juan" = niceGreeting ++ " Juan!"
greet "Fernando" = niceGreeting ++ " Fernando!"
greet name = badGreeting ++ " " ++ name
    where niceGreeting = "Hello! So very nice to see you,"
          badGreeting = "Oh! Pfft. It's you."
```
- O
```haskell
badGreeting :: String
badGreeting = "Oh! Pfft. It's you."

niceGreeting :: String
niceGreeting = "Hello! So very nice to see you,"

greet :: String -> String
greet "Juan" = niceGreeting ++ " Juan!"
greet "Fernando" = niceGreeting ++ " Fernando!"
greet name = badGreeting ++ " " ++ name
```

### where와 함께하는 패턴 매칭
```haskell
...
where bmi = weight / height ^ 2
      (skinny, normal, fat) = (18.5, 25.0, 30.0)
```
- 이름과 성을 받아서 이니셜을 반환하는 함수
```haskell
initials :: String -> String -> String
initials firstname lastname = [f] ++ ". " ++ [l] ++ "."
  where (f:_) = firstname
        (l:_) = lastname
```

### where 블록 속의 함수
```haskell
calcBmis :: [(Double, Double)] -> [Double]
calcBmis xs = [bmi w h | (w, h) <- xs]
  where bmi weight height = weight / height ^ 2
```

## let (표현식)
- 어디서든지 변수와 바인딩
- 패턴 매칭에서 사용
- 영역이 로컬이기 때문에 가드(guard)에서 사용 불가
```haskell
cylinder :: Double -> Double -> Double
cylinder r h =
  let sideArea = 2 * pi * r * h
      topArea = pi * r ^ 2
  in sideArea + 2 * topArea
```

```haskell
ghci> 4 * (let a = 9 in a + 1) + 2
42
```

- 로컬 영역의 함수를 실행하기 위하여 사용
```haskell
ghci> [let square x = x * x in (square 5, square 3, square 2)]
[(25,9,4)]
```

- 여러 변수들을 바인딩
```haskell
ghci> (let a = 100; b = 200; c = 300 in a*b*c, let foo="Hey "; bar = "there!" in foo ++ bar)
(6000000,"Hey there!")
```

- let 표현식으로 하는 패턴 매칭은 튜플을 빠르게 해체, 바인딩 하는데 유용
```haskell
ghci> (let (a,b,c) = (1,2,3) in a+b+c) * 100
600
```

### 리스트 통합에서의 let
```haskell
calcBmis :: [(Double, Double)] -> [Double]
calcBmis xs = [bmi | (w, h) <- xs, let bmi = w / h ^ 2]
```

- (w, h) <- xs는 제네레이터(generator)
```haskell
calcBmis :: [(Double, Double)] -> [Double]
calcBmis xs = [bmi | (w, h) <- xs, let bmi = w / h ^ 2, bmi > 25.0]
```

### GHCi에서의 let
```haskell
ghci> let zoot x y z = x * y + z
ghci> zoot 3 9 2
29
ghci> let boot x y z = x * y + z in boot 3 4 2
14
ghci> boot
<interactive>:69:1: error:
    • Variable not in scope: boot
    • Perhaps you meant ‘zoot’ (line 66)
```

## case 표현식
- 특정 변수의 특정 값에 대한 코드 블록을 실행할 수 있게 해준다
- 코드 내 어디서든지 패턴 매칭을 사용하기 위한 방법
- 표현식을 평가할 수 있을 뿐만 아니라, 패턴 매칭도 할 수 있다.
```haskell
head' :: [a] -> a
head' [] = error "No head for empty lists!"
head' (x:_) = x
```

```haskell
head' :: [a] -> a
head' xs = case xs of [] -> error "No head for empty lists!"
                      (x:_) -> x
```

```
case 표현식 패턴 -> 결과
          패턴 -> 결과
          패턴 -> 결과
```

- case표현식
- ???
```haskell
describeList :: [a] -> String
describeList ls = "The list is " ++ case ls of [] -> "empty"
  [x] -> "a singleton list."
  xs -> "a longer list."
```

```haskell
describeList :: [a] -> String
describeList ls = "The list is " ++ what ls
  where what [] = "empty."
        what [x] = "a singleton list"
        what xs = "a longer list."
```